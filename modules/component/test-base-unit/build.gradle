plugins {
	id("net.twisterrob.astro.library")
}

dependencies {
	api(platform(libs.test.jupiter.bom)) {
		because("Platform, Jupiter, and Vintage versions should match.")
	}
	api(libs.test.jupiter.api) {
		because("Provides JUnit 5 @Test annotation for tests.")
	}
	api(libs.test.jupiter.params) {
		because("Provides JUnit 5 @ParameterizedTest annotation for tests.")
	}
	runtimeOnly(libs.test.jupiter.engine) {
		because("JUnit 5 engine is needed explicitly to run tests in Gradle.")
	}
	runtimeOnly(libs.test.jupiter.launcher) {
		because("JUnit 5 launcher is needed explicitly to run tests in Gradle.")
	}

	api(libs.test.kotest.assertions) {
		because("Choice of assertions library for this project.")
	}
	api(platform(libs.test.mockito.bom)) {
		because("Mockito versions should match.")
	}
	api(libs.test.mockito.kotlin) {
		because("Choice of mocking library for this project.")
	}
}
